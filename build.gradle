buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.1.0'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'com.github.hierynomus.license' version '0.16.1'
}

// Default tasks
defaultTasks 'licenseFormat', 'check', 'build'

// Basic project information
group = 'net.fabricmc'
archivesBaseName = 'sponge-mixin'
version = buildVersion + "+mixin." + upstreamMixinVersion


def ENV = System.getenv()
if (!ENV.CI) {
    version = version + "-local"
}

// Extended project information
ext.projectName = 'Mixin'
ext.inceptionYear = '2014'
ext.packaging = 'jar'

// Define variables
ext.asmVersion = project.hasProperty("asmVersion") ? asmVersion : '9.0'
ext.legacyForgeAsmVersion = project.hasProperty("legacyForgeAsmVersion") ? asmVersion : '5.0.3'

// Minimum version of Java required
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// Project repositories
repositories {
    mavenCentral()
    maven {
        name = 'minecraft'
        url = 'https://libraries.minecraft.net/'
    }
    maven {
        name = 'fabric'
        url = 'https://maven.fabricmc.net/'
    }
    maven {
        // For modlauncher
        name = 'forge'
        url = 'https://files.minecraftforge.net/maven'
    }
}

configurations {
    thinJar
    stagingJar
    exampleImplementation {
        extendsFrom implementation
    }
    fernflowerImplementation {
        extendsFrom implementation
    }
    launchwrapperImplementation {
        extendsFrom implementation
    }
    modlauncherImplementation {
        extendsFrom implementation
    }
    agentImplementation {
        extendsFrom implementation
    }
}

// Include annotation processor sourceSet
sourceSets {
    legacy
    main {
        compileClasspath += legacy.output
    }
    ap {
        compileClasspath += main.output
    }
    fernflower {
        compileClasspath += main.output
    }
    agent {
        compileClasspath += main.output
    }
    bridge {
        compileClasspath += main.output
    }
    example {
        compileClasspath += main.output
        compileClasspath += ap.output
    }
    launchwrapper {
        compileClasspath += main.output
    }
    modlauncher {
        compileClasspath += main.output
    }
}

// Project dependencies
dependencies {
    def guava = 'com.google.guava:guava:21.0' // from mc1.12 onwards
    def log4j2 = 'org.apache.logging.log4j:log4j-core:2.0-beta9'
    def gson = 'com.google.code.gson:gson:2.2.4'

    implementation guava
    implementation log4j2
    implementation gson

    stagingJar guava
    stagingJar gson

    if (Float.parseFloat(asmVersion) < 6) {
        implementation "org.ow2.asm:asm-debug-all:$asmVersion"
    }
    implementation "org.ow2.asm:asm-tree:$asmVersion"
    implementation "org.ow2.asm:asm-commons:$asmVersion"
    implementation "org.ow2.asm:asm-util:$asmVersion"

    thinJar "org.ow2.asm:asm-tree:$asmVersion"
    thinJar "org.ow2.asm:asm-util:$asmVersion"

    // Tests
//    testCompile 'junit:junit:4.11'
//    testCompile 'org.hamcrest:hamcrest-library:1.3'
//    testCompile 'org.mockito:mockito-core:1.9.0'
    
    // Annotation Processor
    apImplementation "org.ow2.asm:asm-tree:$asmVersion"
    apImplementation guava

    // Fernflower decompiler
    fernflowerImplementation 'org.jetbrains:intellij-fernflower:1.0.0.9'

    // LegacyLauncher service
    launchwrapperImplementation ('net.minecraft:launchwrapper:1.11') {
        exclude module: 'lwjgl'
    }

    modlauncherImplementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    modlauncherImplementation "org.ow2.asm:asm-tree:$modlauncherAsmVersion"
    modlauncherImplementation "org.ow2.asm:asm-commons:$modlauncherAsmVersion"
    modlauncherImplementation "org.ow2.asm:asm-util:$modlauncherAsmVersion"
    modlauncherImplementation ("cpw.mods:modlauncher:$modlauncherVersion") {
        exclude module: 'log4j-core'
    }
    modlauncherImplementation 'cpw.mods:grossjava9hacks:1.3.+'

    // asm bridge
    bridgeImplementation log4j2
    bridgeImplementation "org.ow2.asm:asm-commons:$legacyForgeAsmVersion"
    
    legacyImplementation "org.ow2.asm:asm-tree:$asmVersion"
}

javadoc {
    exclude '**/throwables'
    classpath += sourceSets.legacy.output
    source sourceSets.ap.allJava
    options.encoding = 'UTF-8'
    exclude {
        it.relativePath.file && it.relativePath.pathString =~ 'tools' && !(it.name =~ /SuppressedBy|package-info/) }
    options {
        docTitle 'Welcome to the Mixin Javadoc'
        overview 'docs/javadoc/overview.html'
        stylesheetFile file('docs/javadoc/mixin.css')
        addBooleanOption '-allow-script-in-comments', true
    }
    doLast {
        copy {
            from 'docs/javadoc/resources'
            into outputDirectory
        }
    }
}

// Eclipse
eclipse.classpath.plusConfigurations += [
    configurations.fernflowerCompileClasspath,
    configurations.launchwrapperCompileClasspath,
    configurations.modlauncherCompileClasspath,
    configurations.bridgeCompileClasspath
]

// IntelliJ
idea.module.scopes.COMPILE.plus += [
    configurations.fernflowerCompileClasspath,
    configurations.launchwrapperCompileClasspath,
    configurations.modlauncherCompileClasspath,
    configurations.bridgeCompileClasspath
]

// Filter, process, and include resources
processResources {
    // Include in final JAR
   from 'LICENSE.txt'
}

// License header formatting
license {
    ext {
        name = "Mixin"
        organization = "SpongePowered"
        url = "https://www.spongepowered.org"
    }
    include '**/*.java'
    header file("HEADER.txt")
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

checkstyle {
    configProperties = [
        "name"        : project.name,
        "organization": project.organization,
        "url"         : project.url,
        "year"        : project.inceptionYear
    ]
    configFile = file("checkstyle.xml")
    toolVersion = '8.43'
}

// Source compiler configuration
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-proc:none']
    options.deprecation = true
    options.encoding = 'utf8'
    if (JavaVersion.current().isJava9Compatible()) {
        options.release = 8
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task stagingJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    sourceSets.each {
        if (it.name != 'example') {
            from it.output
        }
    }
    
    configurations = [project.configurations.stagingJar]
    
    // JAR manifest configuration
    manifest.attributes(
        "Built-By": System.properties['user.name'],
        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title": name,
        "Implementation-Version": archiveVersion,
        "Implementation-Vendor": url,
        // for hotswap agent
        "Premain-Class": "org.spongepowered.tools.agent.MixinAgent",
        "Agent-Class": "org.spongepowered.tools.agent.MixinAgent",
        "Can-Redefine-Classes": true,
        "Can-Retransform-Classes": true
    )
    
    mergeServiceFiles()
    relocate 'com.google', 'org.spongepowered.include.com.google'
    archiveClassifier = "staging"
}

task stagingProguardJar(type: proguard.gradle.ProGuardTask, dependsOn: stagingJar) {
    doFirst {
        configurations.compileClasspath.resolve().forEach {
            libraryjars it
        }
    }

    libraryjars JavaVersion.current().java9Compatible ? "${System.getProperty('java.home')}/jmods" : "${System.getProperty('java.home')}lib/rt.jar"

    injars stagingJar.archiveFile
    outjars file("build/libs/sponge-mixin-${version}.jar")
    configuration file("proguard.conf")
}
build.dependsOn stagingProguardJar

// Clear artifacts because jar will be there by default and we want to use staging jar instead
configurations.archives.artifacts.clear()

task thinJar(type: Jar) {
    from sourceSets.main.output
//    from { configurations.thinJar.collect { zipTree(it) }
    archiveBaseName = 'sponge-mixin-runtime'
    archiveClassifier = ''
    
    exclude 'org/spongepowered/asm/launch/**'
    exclude 'org/spongepowered/asm/mixin/injection/invoke/**'
    exclude 'org/spongepowered/asm/mixin/transformer/*.class'
    exclude 'org/spongepowered/asm/mixin/transformer/debug/**'
    exclude 'org/spongepowered/asm/mixin/transformer/verify/**'
    
    manifest.mainAttributes(
        "Built-By": System.properties['user.name'],
        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title": name,
        "Implementation-Version": archiveVersion,
        "Implementation-Vendor": url,
    )
}
build.dependsOn(thinJar)

// generate shadow jar so we can use the AP standalone
shadowJar  {
    from sourceSets.ap.output
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    archiveClassifier = 'processor'
}
build.dependsOn(shadowJar)

// Run this task instead of build to generate a timestamped shadow jar (for dev)
task timestamp(type: Jar, dependsOn: build) {
    if (gradle.startParameter.taskNames.contains(name)) {
        shadowJar.archiveClassifier = new Date().format('yyyyMMddHHmmss')
    }
}

task sourceJar(type: Jar) {
    sourceSets.each {
        from it.java
        from it.resources
    }
    archiveClassifier = "sources"
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	from javadoc.destinationDir
    archiveClassifier = "javadoc"
}

artifacts {
    archives stagingJar
    archives thinJar
    archives sourceJar
    archives javadocJar
    archives shadowJar
}

ext.excludePomDeps = [
    'fernflower',
    'jarjar',
    'hamcrest-library',
    'junit',
    'mockito-core',
    'mixin-asm-debug-all',
    'log4j-core'
]

publishing {
    publications {
        developer(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version project.version

            artifact sourceJar
            artifact javadocJar
            artifact(file("build/libs/sponge-mixin-${version}.jar")) {
                builtBy stagingProguardJar
                classifier = null
            }

            // https://issues.gradle.org/browse/GRADLE-2966
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.runtimeClasspath.allDependencies.each {
                    if (it.group != null && it.name != null && !excludePomDeps.contains(it.name)) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)

                        if (it.excludeRules.size() > 0) {
                            def exclusionsNode = dependencyNode.appendNode('exclusions')
                            it.excludeRules.each { rule ->
                                def exclusionNode = exclusionsNode.appendNode('exclusion')
                                exclusionNode.appendNode('groupId', rule.group)
                                exclusionNode.appendNode('artifactId', rule.module)
                            }
                        }
                    }
                }
            }
        }

        runtime(MavenPublication) { publication ->
            groupId project.group
            artifactId "sponge-mixin-runtime"
            version project.version

            artifact thinJar

            // https://issues.gradle.org/browse/GRADLE-2966
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.runtimeClasspath.allDependencies.each {
                    if (it.group != null && it.name != null && !excludePomDeps.contains(it.name)) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)

                        if (it.excludeRules.size() > 0) {
                            def exclusionsNode = dependencyNode.appendNode('exclusions')
                            it.excludeRules.each { rule ->
                                def exclusionNode = exclusionsNode.appendNode('exclusion')
                                exclusionNode.appendNode('groupId', rule.group)
                                exclusionNode.appendNode('artifactId', rule.module)
                            }
                        }
                    }
                }
            }
        }
    }

    repositories {
        if (ENV.MAVEN_URL) {
            maven {
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USERNAME
                    password ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
    doFirst {
        def xml = new URL("https://maven.fabricmc.net/net/fabricmc/sponge-mixin/maven-metadata.xml").text
        def metadata = new XmlSlurper().parseText(xml)
        def versions = metadata.versioning.versions.version*.text();
        if (versions.contains(version)) {
            throw new RuntimeException("${version} has already been released!")
        }
    }
}

publish.mustRunAfter checkVersion